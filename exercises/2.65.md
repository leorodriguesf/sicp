## Exercise 2.65

Use the results of [Exercise 2.63](./2.63.md) and [Exercise 2.64](./2.64.md) to
give $Θ(n)$ implementations of `union-set` and `intersection-set` for sets
implemented as (balanced) binary trees.

## Solution

The key to solve this problem is to think in terms of pipelines, not trees.
Because we already have means to transform trees to ordered lists and
vice-versa, and we already know how to `union-set` and `intersection-set`
ordered lists, we can transform the problem into a simpler domain, solve it
there in linear time, and transform back.

The key observation to have an algorithm that also runs in $Θ(n)$ is to realize
that:

- Turning each balanced binary tree into a list is $Θ(n)$
- Merging two lists is $Θ(n)$
- Turning the result back into a tree is $Θ(n)$

The total cost is:

$$
T(n) = Θ(n) + Θ(n) + Θ(n) = 3 \cdot Θ(n)
$$

Since we can drop the factors in asymptotic analysis:

$$
T(n) = Θ(n)
$$

Below we have a simpler version illustrating the idea of a pipeline:

- `union-set-as-ol`: `union-set` from [Exercise 2.62](./2.62.md), just renamed
- `intersection-set-as-ol`: `intersection-set` from the book , just renamed
- `tree->list`: `tree->list2` from the book, just renamed

```scheme
(define (union-set set1 set2)
  (list->tree (union-set-as-ol (tree->list set1) (tree->list set2))))

(define (intersection-set set1 set2)
  (list->tree (intersection-set-as-ol (tree->list set1) (tree->list set2))))
```

The whole solution is shown below:

```scheme
(define (tree->list tree)
  (define (copy-to-list tree result-list)
    (if (null? tree)
        result-list
        (copy-to-list
         (left-branch tree)
         (cons (entry tree)
               (copy-to-list
                (right-branch tree)
                result-list)))))
  (copy-to-list tree '()))

(define (list->tree elements)
  (car (partial-tree
        elements (length elements))))

(define (entry tree) (car tree))
(define (left-branch tree) (cadr tree))
(define (right-branch tree) (caddr tree))
(define (make-tree entry left right)
  (list entry left right))

(define (partial-tree elts n)
  (if (= n 0)
      (cons '() elts)
      (let ((left-size
             (quotient (- n 1) 2)))
        (let ((left-result
               (partial-tree
                elts left-size)))
          (let ((left-tree
                 (car left-result))
                (non-left-elts
                 (cdr left-result))
                (right-size
                 (- n (+ left-size 1))))
            (let ((this-entry
                   (car non-left-elts))
                  (right-result
                   (partial-tree
                    (cdr non-left-elts)
                    right-size)))
              (let ((right-tree
                     (car right-result))
                    (remaining-elts
                     (cdr right-result)))
                (cons (make-tree this-entry
                                 left-tree
                                 right-tree)
                      remaining-elts))))))))

(define (union-set set1 set2)
  (define (union-set-as-ol set-ol-1 set-ol-2)
    (cond ((null? set-ol-1) set-ol-2)
          ((null? set-ol-2) set-ol-1)
          (else
            (let ((x1 (car set-ol-1))
                  (x2 (car set-ol-2)))
              (cond ((= x1 x2) (cons x1 (union-set-as-ol (cdr set-ol-1)
                                                         (cdr set-ol-2))))
                    ((< x1 x2) (cons x1 (union-set-as-ol (cdr set-ol-1)
                                                         set-ol-2)))
                    (else (cons x2 (union-set-as-ol set-ol-1
                                                    (cdr set-ol-2)))))))))
  (list->tree (union-set-as-ol (tree->list set1) (tree->list set2))))

(define (intersection-set set1 set2)
  (define (intersection-set-as-ol set-ol-1 set-ol-2)
    (if (or (null? set-ol-1) (null? set-ol-2))
      '()
      (let ((x1 (car set-ol-1))
            (x2 (car set-ol-2)))
        (cond ((= x1 x2) (cons x1 (intersection-set-as-ol (cdr set-ol-1)
                                                          (cdr set-ol-2))))
              ((< x1 x2) (intersection-set-as-ol (cdr set-ol-1) set-ol-2))
              (else (intersection-set-as-ol set-ol-1 (cdr set-ol-2)))))))
  (list->tree (intersection-set-as-ol (tree->list set1) (tree->list set2))))
```
