## Exercise 2.68

The `encode` procedure takes as arguments a message and a tree and produces the
list of bits that gives the encoded message.

```scheme
(define (encode message tree)
  (if (null? message)
      '()
      (append
       (encode-symbol (car message)
                      tree)
       (encode (cdr message) tree))))
```

`Encode-symbol` is a procedure, which you must write, that returns the list of
bits that encodes a given symbol according to a given tree. You should design
`encode-symbol` so that it signals an error if the symbol is not in the tree at
all. Test your procedure by encoding the result you obtained in
[Exercise 2.67](./2.67.md) with the sample tree and seeing whether it is the
same as the original sample message.

## Solution

First, we write the `encode-symbol`:

```scheme
(define (element-of-set? x set)
  (cond ((null? set) #f)
        ((equal? x (car set)) #t)
        (else (element-of-set? x (cdr set)))))

(define (encode message tree)
  (define (encode-symbol symbol current-branch)
    (cond ((leaf? current-branch)
            (if (equal? symbol (symbol-leaf current-branch))
              '() ; This is the symbol - done
              (error "symbol not in the leaf:
                      ENCODE-SYMBOL" symbol)))
          ((element-of-set? symbol (symbols (left-branch current-branch)))
           (cons 0 (encode-symbol symbol (left-branch current-branch))))
          ((element-of-set? symbol (symbols (right-branch current-branch)))
           (cons 1 (encode-symbol symbol (right-branch current-branch))))
          (else (error "symbol not in the tree:
                       ENCODE-SYMBOL" symbol))))
  (if (null? message)
      '()
      (append
       (encode-symbol (car message)
                      tree)
       (encode (cdr message) tree))))
```

Now we can test and compare with the result from [Exercise 2.67](./2.67.md):

```
> sample-message
(0 1 1 0 0 1 0 1 0 1 1 1 0)
> (equal?
  sample-message
  (encode (decode sample-message sample-tree)
          sample-tree))
#t
```
