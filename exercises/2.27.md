## Exercise 2.27

Modify your `reverse` procedure of [Exercise 2.18](./2.18) to produce a
`deep-reverse` procedure that takes a list as argument and returns as its value
the list with its elements reversed and with all sublists deep-reversed as well.
For example,

```scheme
(define x
  (list (list 1 2) (list 3 4)))

x
((1 2) (3 4))

(reverse x)
((3 4) (1 2))

(deep-reverse x)
((4 3) (2 1))
```

## Solution

The exercise invites the reader to modify the `reverse` procedure to produce a
new `deep-reverse`. To do this we write:

```scheme
(define x
  (list (list 1 2) (list 3 4)))

(define (deep-reverse lol)
  (if (null? lol)
      '()
      (append (deep-reverse (cdr lol))
              (list (if (list? (car lol))
                      (deep-reverse (car lol))
                      (car lol))))))

```

But a more elegant solution would benefit from the fact that `reverse` is
already defined, and use it as a building block to define `deep-reverse`:

```scheme
(define x
  (list (list 1 2) (list 3 4)))

(define (reverse lst)
  (if (null? lst)
      '()
      (append (reverse (cdr lst)) (list(car lst)))))

(define (deep-reverse lol)
  (if (list? lol)
    (map deep-reverse (reverse lol))
    lol))
```
