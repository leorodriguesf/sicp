## Exercise 2.4

Here is an alternative procedural representation of pairs. For this representation, verify that `(car (cons x y))` yields `x` for any objects `x` and `y`.

```
(define (cons x y)
  (lambda (m) (m x y)))

(define (car z)
  (z (lambda (p q) p)))
```

What is the corresponding definition of cdr? (Hint: To verify that this works, make use of the substitution model of 1.1.5.)

## Solution

Lets apply the substitution model for `(car (cons 1 2))`:

```scheme
(car (cons 1 2))
```

```scheme
(car (lambda (m) (m 1 2)))
```

```scheme
((lambda (m) (m 1 2)) (lambda (p q) p))
```

This means that we will call the procedure `(lambda (m) (m 1 2))` which binds `m` to `(lambda (p q) p)` resulting in:

```scheme
((lambda (p q) p) 1 2)
> 1
```

Likewise, we can write the `cdr` procedure just changing the procedure to return `q` instead of `p`:

```scheme
(define (cdr z)
  (z (lambda (p q) q)))
```

Using the substitution model for `(cdr (cons 1 2))`:

```scheme
(cdr (cons 1 2))
```

```scheme
(cdr (lambda (m) (m 1 2)))
```

```scheme
((lambda (m) (m 1 2)) (lambda (p q) q))
```

```scheme
((lambda (p q) q) 1 2)
> 2
```
