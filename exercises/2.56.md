## Exercise 2.56

Show how to extend the basic differentiator to handle more kinds of expressions.
For instance, implement the differentiation rule

$$
\frac{d(u^n)}{dx} = nu^{n-1}\frac{du}{dx}
$$

by adding a new clause to the `deriv` program and defining appropriate
procedures `exponentiation?`, `base`, `exponent`, and `make-exponentiation`.
(You may use the symbol `**` to denote exponentiation.) Build in the rules that
anything raised to the power 0 is 1 and anything raised to the power 1 is the
thing itself.

## Solution

```scheme
(define ** expt)

(define (deriv exp var)
  (cond ((number? exp) 0)
        ((variable? exp)
         (if (same-variable? exp var) 1 0))
        ((sum? exp)
         (make-sum (deriv (addend exp) var)
                   (deriv (augend exp) var)))
        ((product? exp)
         (make-sum
           (make-product
             (multiplier exp)
             (deriv (multiplicand exp) var))
           (make-product
             (deriv (multiplier exp) var)
             (multiplicand exp))))
        ((and (exponentiation? exp)
              (number? (exponent exp)))
         (make-product
           (exponent exp)
           (make-product (make-exponentiation (base exp) (- (exponent exp) 1))
                         (deriv (base exp) var))))
        (else (error "unknown expression
                     type: DERIV" exp))))

(define (variable? x) (symbol? x))

(define (same-variable? x y)
  (and (variable? x)
       (variable? y)
       (eq? x y)))

(define (sum? exp)
  (and (pair? exp)
       (eq? (car exp) '+)))

(define (make-sum x y)
  (cond ((=number? x 0) y)
        ((=number? y 0) x)
        ((and (number? x)
              (number? y))
         (+ x y))
        (else (list '+ x y))))
(define (addend exp) (cadr exp))
(define (augend exp) (caddr exp))

(define (product? exp)
  (and (pair? exp)
       (eq? (car exp) '*)))

(define (make-product x y)
  (cond ((or (=number? x 0)
             (=number? y 0))
         0)
        ((=number? x 1) y)
        ((=number? y 1) x)
        ((and (number? x)
              (number? y))
         (* x y))
        (else (list '* x y))))

(define (multiplier exp) (cadr exp))
(define (multiplicand exp) (caddr exp))

(define (exponentiation? exp)
  (and (pair? exp)
       (eq? (car exp) '**)))

(define (base exp) (cadr exp))
(define (exponent exp) (caddr exp))

(define (make-exponentiation x y)
  (cond ((=number? y 0) 1)
        ((=number? y 1) x)
        ((=number? x 0) 0)
        ((=number? x 1) 1)
        ((and (number? x)
              (number? y))
         (** x y))
        (else (list '** x y))))


(define (=number? exp num)
  (and (number? exp)
       (= exp num)))
```
