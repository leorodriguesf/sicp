## Exercise 2.5

Show that we can represent pairs of nonnegative integers using only numbers and arithmetic operations if we represent the pair $a$ and $b$ as the integer that is the product ${2^a3^b}$. Give the corresponding definitions of the procedures `cons`, `car`, and `cdr`.

## Solution

The key is to realize that the product ${2^a3^b}$ will be a number `n` such that we can check how many times we can divide evenly the number `n` by 2 to get the `car` and by 3 to get the `cdr`.

For example, if $a = 5$ and $b = 4$:

$$
2^53^4 = 2 \times 2 \times 2 \times 2 \times 2 \times 3 \times 3 \times 3 \times 3 = 2592
$$

In this case $2592$ can be divided by 2, evenly, 5 times:

$$
2592 \div 2 = 1296
$$

$$
1296 \div 2 = 648
$$

$$
648 \div 2 = 324
$$

$$
324 \div 2 = 162
$$

$$
162 \div 2 = 81
$$

And, $2592$ can be divided by 3, evenly, 4 times:

$$
2592 \div 3 = 864
$$

$$
864 \div 3 = 288
$$

$$
288 \div 3 = 96
$$

$$
96 \div 3 = 32
$$

Note that this method will work not only with 2 and 3, but for any relative prime numbers.

Given the definition we can write the procedures:

```scheme
(define (cons a b)
  (* (expt 2 a) (expt 3 b)))

(define (count-divisions n p)
  (define (iter x count)
    (if (= 0 (remainder x p))
        (iter (/ x p) (+ count 1))
        count))
  (iter n 0))

(define (car n) (count-divisions n 2))

(define (cdr n) (count-divisions n 3))
```
