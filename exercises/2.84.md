## Exercise 2.84

Using the `raise` operation of [Exercise 2.83](./2.83.md), modify the
`apply-generic` procedure so that it coerces its arguments to have the same type
by the method of successive raising, as discussed in this section. You will need
to devise a way to test which of two types is higher in the tower. Do this in a
manner that is “compatible” with the rest of the system and will not lead to
problems in adding new levels to the tower.

## Solution

```scheme
(define (apply-generic op . args)

  (define (try-raise-to x target-type)
    (cond ((eq? (type-tag x) target-type) x)
          (else
            (let ((raise-proc (get 'raise (list (type-tag x)))))
              (if raise-proc
                (try-raise-to (raise-proc (contents x)) target-type)
                #f)))))

  (define (can-raise-to? x target-type)
    (not (not (try-raise-to x target-type))))

  (define (try-find-higher-in-type x y)
    (cond ((eq? (type-tag x) (type-tag y)) x)
          ((can-raise-to? x (type-tag y)) y)
          ((can-raise-to? y (type-tag x)) x)
          (else #f)))

  (define (try-find-highest-in-type args)
    (define (iter lst highest)
      (if (null? lst)
        highest
        (let ((next (try-find-higher-in-type highest (car lst))))
          (if next
            (iter (cdr lst) next)
            #f))))
    (if (null? args)
      #f
      (iter (cdr args) (car args))))

  (define (raise-all-to args target-type)
    (map (lambda (x)
           (try-raise-to x target-type))
         args))

  (let* ((type-tags (map type-tag args))
         (vals (map contents args))
         (proc (get op type-tags)))
    (define (err)
      (error
        "No method for these types"
        (list op type-tags)))
    (if proc
      (apply proc vals)
      (let ((highest (try-find-highest-in-type args)))
        (if highest
          (let* ((all-raised (raise-all-to args (type-tag highest)))
                 (all-raised-type-tags (map type-tag all-raised))
                 (new-proc (get op all-raised-type-tags)))
            (if new-proc
              (apply new-proc (map contents all-raised))
              (err)))
          (err))))))
```
