## Exercise 2.57

Extend the differentiation program to handle sums and products of arbitrary
numbers of (two or more) terms. Then the last example above could be expressed
as

```scheme
(deriv '(* x y (+ x 3)) 'x)
```

Try to do this by changing only the representation for sums and products,
without changing the `deriv` procedure at all. For example, the `addend` of a
sum would be the first term, and the `augend` would be the sum of the rest of
the terms.

## Solution

Because our differentiation program is using abstract data to represent sums and
prodcuts, we only need a small change on the selectors:

```diff
-(define (augend exp) (caddr exp))
+(define (augend exp)
+ (if (null? (cddr exp))
+   0
+   (cons '+ (cddr exp))))

-(define (multiplicand exp) (caddr exp))
+(define (multiplicand exp)
+ (if (null? (cddr exp))
+   1
+   (cons '* (cddr exp))))
```

The result is shown below:

```scheme
(define ** expt)

(define (deriv exp var)
  (cond ((number? exp) 0)
        ((variable? exp)
         (if (same-variable? exp var) 1 0))
        ((sum? exp)
         (make-sum (deriv (addend exp) var)
                   (deriv (augend exp) var)))
        ((product? exp)
         (make-sum
           (make-product
             (multiplier exp)
             (deriv (multiplicand exp) var))
           (make-product
             (deriv (multiplier exp) var)
             (multiplicand exp))))
        ((and (exponentiation? exp)
              (number? (exponent exp)))
         (make-product
           (exponent exp)
           (make-product (make-exponentiation (base exp) (- (exponent exp) 1))
                         (deriv (base exp) var))))
        (else (error "unknown expression
                     type: DERIV" exp))))

(define (variable? x) (symbol? x))

(define (same-variable? x y)
  (and (variable? x)
       (variable? y)
       (eq? x y)))

(define (sum? exp)
  (and (pair? exp)
       (eq? (car exp) '+)))

(define (make-sum x y)
  (cond ((=number? x 0) y)
        ((=number? y 0) x)
        ((and (number? x)
              (number? y))
         (+ x y))
        (else (list '+ x y))))
(define (addend exp) (cadr exp))
(define (augend exp)
  (if (null? (cddr exp))
    0
    (cons '+ (cddr exp))))

(define (product? exp)
  (and (pair? exp)
       (eq? (car exp) '*)))

(define (make-product x y)
  (cond ((or (=number? x 0)
             (=number? y 0))
         0)
        ((=number? x 1) y)
        ((=number? y 1) x)
        ((and (number? x)
              (number? y))
         (* x y))
        (else (list '* x y))))

(define (multiplier exp) (cadr exp))
(define (multiplicand exp)
 (if (null? (cddr exp))
   1
   (cons '* (cddr exp))))

(define (exponentiation? exp)
  (and (pair? exp)
       (eq? (car exp) '**)))

(define (base exp) (cadr exp))
(define (exponent exp) (caddr exp))

(define (make-exponentiation x y)
  (cond ((=number? y 0) 1)
        ((=number? y 1) x)
        ((=number? x 0) 0)
        ((=number? x 1) 1)
        ((and (number? x)
              (number? y))
         (** x y))
        (else (list '** x y))))


(define (=number? exp num)
  (and (number? exp)
       (= exp num)))
```
