## Exercise 2.29

A binary mobile consists of two branches, a left branch and a right branch. Each
branch is a rod of a certain length, from which hangs either a weight or another
binary mobile. We can represent a binary mobile using compound data by
constructing it from two branches (for example, using `list`):

```scheme
(define (make-mobile left right)
  (list left right))
```

A branch is constructed from a `length` (which must be a number) together with a
`structure`, which may be either a number (representing a simple weight) or
another mobile:

```scheme
(define (make-branch length structure)
  (list length structure))
```

1. Write the corresponding selectors `left-branch` and `right-branch`, which
   return the branches of a mobile, and `branch-length` and `branch-structure`,
   which return the components of a branch.
2. Using your selectors, define a procedure `total-weight` that returns the
   total weight of a mobile.
3. A mobile is said to be `balanced` if the torque applied by its top-left
   branch is equal to that applied by its top-right branch (that is, if the
   length of the left rod multiplied by the weight hanging from that rod is
   equal to the corresponding product for the right side) and if each of the
   submobiles hanging off its branches is balanced. Design a predicate that
   tests whether a binary mobile is balanced.
4. Suppose we change the representation of mobiles so that the constructors are

   ```scheme
   (define (make-mobile left right)
    (cons left right))

   (define (make-branch length structure)
    (cons length structure))
   ```

   How much do you need to change your programs to convert to the new
   representation?

## Solution

1. Write the corresponding selectors `left-branch` and `right-branch`, which
   return the branches of a mobile, and `branch-length` and `branch-structure`,
   which return the components of a branch.

   ```scheme
   (define (make-mobile left right)
    (list left right))

   (define (make-branch length structure)
    (list length structure))

   (define (left-branch mobile)
    (car mobile))

   (define (right-branch mobile)
    (cadr mobile))

   (define (branch-length branch)
    (car branch))

   (define (branch-structure branch)
    (cadr branch))
   ```

2. Using your selectors, define a procedure `total-weight` that returns the
   total weight of a mobile.

   ```scheme
    (define (branch-weight branch)
      (let ((bs (branch-structure branch)))
        (if (pair? bs)
          (total-weight bs)
          bs)))

    (define (total-weight mobile)
      (+ (branch-weight (left-branch mobile))
        (branch-weight (right-branch mobile))))
   ```

3. A mobile is said to be `balanced` if the torque applied by its top-left
   branch is equal to that applied by its top-right branch (that is, if the
   length of the left rod multiplied by the weight hanging from that rod is
   equal to the corresponding product for the right side) and if each of the
   submobiles hanging off its branches is balanced. Design a predicate that
   tests whether a binary mobile is balanced.

   ```scheme
   (define (branch-structure-balanced? branch)
     (let ((bs (branch-structure branch)))
       (if (pair? bs)
         (balanced? bs)
         #t)))

   (define (torque branch)
     (* (branch-length branch)
       (branch-weight branch)))

   (define (balanced? mobile)
     (let ((top-left-branch (left-branch mobile))
           (top-right-branch (right-branch mobile)))
       (and
         (= (torque top-left-branch) (torque top-right-branch))
         (branch-structure-balanced? top-left-branch)
         (branch-structure-balanced? top-right-branch))))
   ```

4. Suppose we change the representation of mobiles so that the constructors are

   ```scheme
   (define (make-mobile left right)
    (cons left right))

   (define (make-branch length structure)
    (cons length structure))
   ```

   How much do you need to change your programs to convert to the new
   representation?

   This is a very interesting question, because it shows the power of defining
   good data abstractions. This change in the program would only require the
   change on the selectors, because data abstraction decouples program logic
   from representation details as long we do not change how we reason about
   them.

   ```scheme
   (define (make-mobile left right)
     (cons left right))

   (define (make-branch length structure)
     (cons length structure))

   (define (left-branch mobile)
     (car mobile))

   (define (right-branch mobile)
     (cdr mobile))

   (define (branch-length branch)
     (car branch))

   (define (branch-structure branch)
     (cdr branch))
   ```
