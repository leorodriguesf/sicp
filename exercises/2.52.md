## Exercise 2.52

Make changes to the square limit of `wave` shown in Figure 2.9 by working at
each of the levels described above. In particular:

1. Add some segments to the primitive `wave` painter of
   [Exercise 2.49](./2.49.md) (to add a smile, for example).
2. Change the pattern constructed by `corner-split` (for example, by using only
   one copy of the `up-split` and `right-split` images instead of two).
3. Modify the version of `square-limit` that uses `square-of-four` so as to
   assemble the corners in a different pattern. (For example, you might make the
   big Mr. Rogers look outward from each corner of the square.)

## Solution

### 1. Add some segments to the primitive `wave` painter of [Exercise 2.49](./2.49.md) (to add a smile, for example)

```scheme
#lang racket

(require sicp-pict)

; From exercise 2.49 start
(define wave-segments
  (list (make-segment (make-vect 0.46 1.00) (make-vect 0.37 0.78))
        (make-segment (make-vect 0.37 0.78) (make-vect 0.46 0.66))
        (make-segment (make-vect 0.46 0.66) (make-vect 0.37 0.67))
        (make-segment (make-vect 0.37 0.67) (make-vect 0.22 0.55))
        (make-segment (make-vect 0.22 0.55) (make-vect 0.00 0.72))
        (make-segment (make-vect 0.00 0.67) (make-vect 0.22 0.45))
        (make-segment (make-vect 0.22 0.45) (make-vect 0.39 0.58))
        (make-segment (make-vect 0.39 0.58) (make-vect 0.31 0.00))
        (make-segment (make-vect 0.54 1.00) (make-vect 0.63 0.78))
        (make-segment (make-vect 0.63 0.78) (make-vect 0.54 0.66))
        (make-segment (make-vect 0.54 0.66) (make-vect 0.63 0.67))
        (make-segment (make-vect 0.63 0.67) (make-vect 1.00 0.33))
        (make-segment (make-vect 1.00 0.28) (make-vect 0.61 0.58))
        (make-segment (make-vect 0.61 0.58) (make-vect 0.69 0.00))
        (make-segment (make-vect 0.39 0.00) (make-vect 0.50 0.32))
        (make-segment (make-vect 0.50 0.32) (make-vect 0.61 0.00))))
; From exercise 2.49 end

(define smile-segments
  (list
    (make-segment (make-vect 0.46 0.87) (make-vect 0.46 0.83))
    (make-segment (make-vect 0.46 0.76) (make-vect 0.50 0.73))
    (make-segment (make-vect 0.54 0.87) (make-vect 0.54 0.83))
    (make-segment (make-vect 0.54 0.76) (make-vect 0.50 0.73))))

(define wave
  (segments->painter wave-segments))

(define wave-variant
  (segments->painter (append wave-segments smile-segments)))

(paint wave)
(paint wave-variant)
```

### 2. Change the pattern constructed by `corner-split` (for example, by using only one copy of the `up-split` and `right-split` images instead of two).

```scheme
#lang racket

(require sicp-pict)

(define (up-split painter n)
  (if (= n 0)
      painter
      (let ((smaller (up-split painter (- n 1))))
        (below painter
               (beside smaller smaller)))))

(define (right-split painter n)
  (if (= n 0)
      painter
      (let ((smaller (right-split painter
                                  (- n 1))))
        (beside painter
                (below smaller smaller)))))

(define (corner-split painter n)
  (if (= n 0)
      painter
      (let ((up (up-split painter (- n 1)))
            (right (right-split painter
                                (- n 1))))
        (let ((top-left (beside up up))
              (bottom-right (below right
                                   right))
              (corner (corner-split painter
                                    (- n 1))))
          (beside (below painter top-left)
                  (below bottom-right
                         corner))))))

(define (corner-split-variant painter n)
  (if (= n 0)
      painter
      (let ((up (up-split painter (- n 1)))
            (right (right-split painter
                                (- n 1))))
        (let ((top-left up)
              (bottom-right right)
              (corner (corner-split painter
                                    (- n 1))))
          (beside (below painter top-left)
                  (below bottom-right
                         corner))))))

(paint (corner-split einstein 1))
(paint (corner-split-variant einstein 1))
```

### 3. Modify the version of `square-limit` that uses `square-of-four` so as to assemble the corners in a different pattern. (For example, you might make the big Mr. Rogers look outward from each corner of the square.)

```scheme
#lang racket

(require sicp-pict)

(define (up-split painter n)
  (if (= n 0)
      painter
      (let ((smaller (up-split painter (- n 1))))
        (below painter
               (beside smaller smaller)))))

(define (right-split painter n)
  (if (= n 0)
      painter
      (let ((smaller (right-split painter
                                  (- n 1))))
        (beside painter
                (below smaller smaller)))))

(define (corner-split painter n)
  (if (= n 0)
      painter
      (let ((up (up-split painter (- n 1)))
            (right (right-split painter
                                (- n 1))))
        (let ((top-left (beside up up))
              (bottom-right (below right
                                   right))
              (corner (corner-split painter
                                    (- n 1))))
          (beside (below painter top-left)
                  (below bottom-right
                         corner))))))

(define (square-of-four tl tr bl br)
  (lambda (painter)
    (let ((top (beside (tl painter)
                       (tr painter)))
          (bottom (beside (bl painter)
                          (br painter))))
      (below bottom top))))

(define (square-limit painter n)
  ((square-of-four flip-horiz
                   identity
                   rotate180
                   flip-vert)
   (corner-split painter n)))

(define (square-limit-variant painter n)
  ((square-of-four identity
                   flip-horiz
                   flip-vert
                   rotate180)
   (corner-split painter n)))

(paint (square-limit einstein 0))
(paint (square-limit-variant einstein 0))
```
